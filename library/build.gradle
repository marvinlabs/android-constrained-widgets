buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4'
        classpath "gradle.plugin.com.vanniktech:gradle-android-javadoc-plugin:0.2.1"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'android-maven'
apply plugin: 'signing'
apply plugin: "com.vanniktech.android.javadoc"

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 24
        versionCode getAppVersionCode()
        versionName getAppVersionName()
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
}

group = "com.marvinlabs"
version = getAppVersionName()

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task jar(type: Jar) {
    from 'build/classes/release'
}

task javadocJar(type: Jar, dependsOn: "generateReleaseJavadoc") {
    classifier = 'javadoc'
    from 'javaDoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives jar
}

task wrapper(type: Wrapper) {
    description "Generates the gradle wrapper"
    gradleVersion = "2.14"
}

signing {
    //TODO: sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: getSonatypeRepositoryUrl()) {
                authentication(userName: getSonatypeRepositoryUser(), password: getSonatypeRepositoryPassword())
            }

            pom.project {
                name 'Android Constrained Widgets'
                artifactId 'android-constrained-widgets'
                packaging 'aar'
                description 'A set of aspect-ratio constrained widgets for Android'
                url 'https://github.com/marvinlabs/android-constrained-widgets'

                scm {
                    url 'scm:git@github.com:marvinlabs/android-constrained-widgets.git'
                    connection 'scm:git@github.com:marvinlabs/android-constrained-widgets.git'
                    developerConnection 'scm:git@github.com:marvinlabs/android-constrained-widgets.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'vpratfr'
                        name 'Vincent Mimoun-Prat'
                    }
                }
            }
        }
    }
}
